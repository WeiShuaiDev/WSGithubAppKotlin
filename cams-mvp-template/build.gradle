apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply from: 'flavors.gradle'
apply plugin: 'kotlin-kapt'

android {
    compileSdkVersion rootProject.ext.android["compileSdkVersion"]
    buildToolsVersion rootProject.ext.android["buildToolsVersion"]

    defaultConfig {
        applicationId "com.linwei.cams_mvp_template"
        minSdkVersion rootProject.ext.android["minSdkVersion"]
        targetSdkVersion rootProject.ext.android["targetSdkVersion"]
        versionCode rootProject.ext.android["versionCode"]
        versionName rootProject.ext.android["versionName"]
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        /**添加多 dex分包支持*/
        multiDexEnabled true
    }
    signingConfigs {
        debug {
            keyAlias "${SIGNING_KEYALIAS}"
            keyPassword "${SIGNGING_CERTIFICATEPASSWORD}"
            storeFile file("${SIGNING_CERTIFICATE}")
            storePassword "${SIGNGING_STOREPASSWORD}"
        }
        release {
            keyAlias "${SIGNING_KEYALIAS}"
            keyPassword "${SIGNGING_CERTIFICATEPASSWORD}"
            storeFile file("${SIGNING_CERTIFICATE}")
            storePassword "${SIGNGING_STOREPASSWORD}"
        }
    }

    buildTypes {
        debug {
            buildConfigField "boolean", "LOG_DEBUG", "true"
            buildConfigField "boolean", "USE_CANARY", "true"
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.debug
        }
        preview {
            buildConfigField "boolean", "LOG_DEBUG", "true"
            buildConfigField "boolean", "USE_CANARY", "true"
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.debug
        }

        release {
            buildConfigField "boolean", "LOG_DEBUG", "false"
            buildConfigField "boolean", "USE_CANARY", "false"
            minifyEnabled false
            shrinkResources false
            zipAlignEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }

    dataBinding {
        enabled true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    android.compileOptions.encoding = "UTF-8"
    android.applicationVariants.all { variant ->
        def outFileName
        def flavorName = variant.flavorName //获取渠道名
        variant.outputs.all { output ->
            def flavorVerName = variant.mergedFlavor.versionName //获取渠道版本名
            if (variant.buildType.name == "release") {//正式
                outFileName = "${flavorName}_${flavorVerName}.${buildTime()}_prd.apk"
            } else if (variant.buildType.name == "preview") {//测试预览
                outFileName = "${flavorName}_${flavorVerName}.${buildTime()}_uat.apk"
            } else {//开发包
                outFileName = "${flavorName}_${flavorVerName}.${buildTime()}_dev.apk"
            }
            output.outputFileName = outFileName
        }
    }

    dexOptions {
        preDexLibraries = false
    }

}


//构建包时间
def buildTime() {
    return new Date().format("MMdd_HHmm", TimeZone.getTimeZone("GMT+8"))
}


dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    kapt rootProject.ext.dependencies["dagger2-compiler"]
    kapt rootProject.ext.dependencies["dagger2-android-processor"]

    implementation project(path: ':cams-mvp')

    //test
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.1'
    testImplementation rootProject.ext.dependencies["junit"]
    androidTestImplementation rootProject.ext.dependencies["test-junit"]
    androidTestImplementation rootProject.ext.dependencies["espresso-core"]
    debugImplementation rootProject.ext.dependencies["canary-debug"]
    releaseImplementation rootProject.ext.dependencies["canary-release"]
    testImplementation rootProject.ext.dependencies["canary-release"]
}
